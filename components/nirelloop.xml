<?xml version="1.0" encoding="utf-8" ?>
<component name="NirelLoop" extends="Scene">

  <children>

    <Video
      id="player"
      enableUI="false"
      visible="true"
      scaling="zoom" />

    <!-- Reinicio cada 3 horas -->
    <Timer
      id="healthRestart"
      duration="10800"
      repeat="true" />

  </children>

  <script type="text/brightscript">
  <![CDATA[

    function init()
      m.top.setFocus(true)

      m.player = m.top.findNode("player")
      m.healthRestart = m.top.findNode("healthRestart")

      rect = m.top.boundingRect()
      m.player.width  = rect.width
      m.player.height = rect.height

      m.player.observeField("state", "onPlayerState")
      m.healthRestart.observeField("fire", "onHealthRestart")

      playVideo()

      m.healthRestart.control = "start"
    end function


    function playVideo()
      content = CreateObject("roSGNode", "ContentNode")
      content.url = "https://heavenlabscr.github.io/nirelads/playlist.m3u8"
      content.streamFormat = "hls"

      m.player.content = content
      m.player.control = "play"
    end function


    function onPlayerState()
      print "Estado video: "; m.player.state

      if m.player.state = "finished"
        playVideo()
      else if m.player.state = "error"
        print "Error detectado, reiniciando..."
        playVideo()
      end if
    end function


    function onHealthRestart()
      print "Reinicio preventivo del player"
      m.player.control = "stop"
      playVideo()
    end function

  ]]>
  </script>
</component>
